// Basic setup

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "[ " ~ (!" ]" ~ ANY) ~ " ]" }

kw_procedure = _{ "PROCEDURE" }
kw_program = _{ "PROGRAM" }
kw_is = _{ "IS" }
kw_begin = _{ "BEGIN" }
kw_var = _{ "VAR" }
kw_end = _{ "END" }

num = @{ ASCII_DIGIT+ }
identifier = @{ ("_" | ASCII_ALPHA_LOWER)+ }

value = { num | identifier }

// Expressions

add = { "+" }
sub = { "-" }
mul = { "*" }
div = { "/" }
rem = { "%" }

expression = {
    value ~ ( add | sub | mul | div | rem ) ~ value |
    value
}

// Conditions

eq = { "=" }
ne = { "!=" }
ge = { ">=" }
le = { "<=" }
gt = { ">" }
lt = { "<" }

condition = { value ~ ( eq | ne | ge | le | gt | lt ) ~ value }

// Commands

cmd_set = { identifier ~ ":=" ~ expression ~ ";" }
cmd_if = { "IF" ~ condition ~ "THEN" ~ commands ~ "ENDIF" }
cmd_if_else = { "IF" ~ condition ~ "THEN" ~ commands ~ "ELSE" ~ commands ~ "ENDIF" }
cmd_while = { "WHILE" ~ condition ~ "DO" ~ commands ~ "ENDWHILE" }
cmd_repeat = { "REPEAT" ~ commands ~ "UNTIL" ~ condition ~ ";" }
cmd_call = { proc_head ~ ";" }
cmd_read = { "READ" ~ identifier ~ ";" }
cmd_write = { "WRITE" ~ value ~ ";" }

command = _{ cmd_set | cmd_if | cmd_if_else | cmd_while | cmd_repeat | cmd_call | cmd_read | cmd_write }

commands = { command+ }

// Sub-structures

declarations = { identifier ~ ("," ~ identifier)* }

proc_head = { identifier ~ "(" ~ declarations ~ ")" }

// Main program structure

function = {
    kw_is ~ kw_var ~ declarations ~ kw_begin ~ commands ~ kw_end | 
    kw_is ~ kw_begin ~ commands ~ kw_end
}

procedure = { kw_procedure ~ proc_head ~ function }

procedures = _{ procedure* }

main = { kw_program ~ function }

program_all = { SOI ~ procedures ~ main ~ EOI }
